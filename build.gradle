plugins {
    id 'groovy'
    id 'java'
    id 'com.github.breadmoirai.github-release' version '2.2.9'
    id 'com.diffplug.gradle.spotless' version '3.25.0'
}

version = '0.0.5'
defaultTasks 'clean','build'
apply plugin: 'java'
apply plugin: 'groovy'
sourceCompatibility = 1.8

def githubToken = System.env.GITHUB_TOKEN

ext.rundeckPluginVersion= '2.0'
ext.rundeckVersion= '3.0.x'
ext.pluginClassNames='com.plugin.pushovernotificationplugin.PushoverNotificationPlugin'


repositories {
    mavenLocal()
    mavenCentral()
    //maven { url "https://jitpack.io" }
}

configurations{
    pluginLibs

    compile{
        extendsFrom pluginLibs
    }
}

dependencies {
    compile 'org.rundeck:rundeck-core:3.0.14+'

    pluginLibs group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
    pluginLibs group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.10'

    testCompile 'junit:junit:4.12'
    testCompile 'org.codehaus.groovy:groovy-all:2.4.15'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
}

// task to copy plugin libs to output/lib dir
task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.pluginLibs
}

jar {
    from "$buildDir/output"
    manifest {
        def libList = configurations.pluginLibs.collect{'lib/'+it.name}.join(' ')

        attributes 'Rundeck-Plugin-Classnames': pluginClassNames
        attributes 'Rundeck-Plugin-File-Version': version
        attributes 'Rundeck-Plugin-Name': 'PushoverNotificationPlugin'
        attributes 'Rundeck-Plugin-Description': 'Sends notifications to pushover.net.'
        attributes 'Rundeck-Plugin-Rundeck-Compatibility-Version': '3.x'
        attributes 'Rundeck-Plugin-Tags': 'java,notification'
        attributes 'Rundeck-Plugin-License': 'Apache 2.0'
        attributes 'Rundeck-Plugin-Source-Link': 'https://github.com/jrbing/pushover-notification-plugin'
        attributes 'Rundeck-Plugin-Target-Host-Compatibility': 'all'
        attributes 'Rundeck-Plugin-Version': rundeckPluginVersion
        attributes 'Rundeck-Plugin-Archive': 'true'
        attributes 'Rundeck-Plugin-Libs': "${libList}"

    }
    dependsOn(copyToLib)
}

wrapper {
    gradleVersion = '5.6.3'
}

githubRelease {
    token "$githubToken"
    owner "jrbing"
    repo "pushover-notification-plugin" // by default this is set to your project name
    targetCommitish "master" // by default this is set to "master"
    releaseAssets configurations.pluginLibs.collect{'lib/'+it.name}.join(' ')
    //jar.destinationDir.listFiles // this points to which files you want to upload as assets with your release
    //tagName "$version" // by default this is set to "v${project.version}"
    //releaseName "v1.0.0" // Release title, by default this is the same as the tagName
    //body "" // by default this is empty
    //draft false // by default this is false
    //prerelease false // by default this is false
    //overwrite true // by default false; if set to true, will delete an existing release with the same tag and name
    //apiEndpoint "https://api.github.com" // should only change for github enterprise users
}

spotless {
    java {
        googleJavaFormat()
    }
}
